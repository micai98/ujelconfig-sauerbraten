// part of ujelconfig by micai
// github.com/micai98/ujelconfig-sauerbraten

newgui main [
	guilist [
		guilist [
			guiplayerpreview $playermodel (m_teammode $getmode) $getweapon [showgui playermodel] 4 0
			newname = (getname)
			guifield newname 15 [name $newname]
			guistrut 0.1
			guilist [
				guibutton (playermodelname) [chooseplayermodel] 0
				guistrut 1
				guiimage (getcrosshair) [showgui uconf_crosshair] 0.5
				]
		]
		guibar
		guilist [		
			guibutton "server browser.."  "showgui servers"
			if (isconnected) [
				guibar
				if (|| $editing (m_edit (getmode))) [
					guibutton "editing.." "showgui editing"
				]
				guibutton "vote game mode / map.." "showgui gamemode"
				// guibutton "lanconnect"
				// guibutton "connect localhost"
				guibutton "switch team" [if (strcmp (getteam) "good") [team evil] [team good]]
				guibutton "toggle spectator" [spectator (! (isspectator (getclientnum)))] "spectator"
				guibutton "master.." [showgui master]
				guibar
				guilist [
					guibutton "options.."  "showgui options"
					guibar
					guibutton "more.."     "showgui uconf"	
				]
				guibutton "disconnect" "disconnect"         "exit"
			] [
				guibutton "bot match.."      "showgui botmatch"
				guibutton "campaign.."       "showgui campaign"
				guibar
				guilist [
					guibutton "options.."  "showgui options"
					guibar
					guibutton "more.."     "showgui uconf"	
				]
				guibutton "about.."          "showgui about"
				guibutton "quit"             "quit"                 "exit"
			]
		]
	]
] 0

newgui uconf_crosshair [
    guitext "crosshair size"
    guislider crosshairsize
    loop row (div (+ (listlen $crosshairs) 6) 7) [
        guilist [
            loop col 7 [
                ch = (at $crosshairs (+ (* $row 7) $col))
                if (!=s $ch "") [
                    guiimage (at $ch 0) [
                        cleargui 1
                        loadcrosshair @(at $ch 0) 0
                        loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
                    ] 0.5
                ]
            ]
        ]
    ]
	guibar

    guilist [
        guitext "mouse sensitivity: "
        newsens = $sensitivity
        guifield newsens 8 [sensitivity $newsens]
    ]

    guilist [
        guitext "mouse UI sensitivity: "
        newguisens = $guisens
        guifield newguisens 8 [guisens $newguisens]
    ]
	
	guicheckbox "invert mouse"      invmouse
] "mouse settings"

newgui notepad [
	guilist [
		guifield notepadfile -30
		guibar
		guistayopen [
			guibutton "new" [ notepadfile = "" ]
		]
	]
    showfileeditor $notepadfile -80 20
]

newgui uconf [
	
	guicheckbox "oqwait (uncheck if game crashes randomly)" oqwait
	guibar
	guilist [
		guibutton notepad [ showgui notepad ]
		guibar
		guibutton scratchpad [ showgui scratchpad ]
		guibar
		guibutton pastebuffer [ showgui pastebuffer ]
	]
	
	guitab "zoom"
	zoom_gui_content
	
	guitab "hud"
	hud_gui_content
	
	guitab "quickattack"
	quickattack_gui_content
	
	guitab "thirdperson"
	thirdperson_gui_content
	
	guitab "fakeweapon"
	fakeweapon_gui_content
	
	guitab about
	guititle "^f0UJELCONFIG"
	guititle (concatword "^f7v" $uconf_version)
	guititle "^f1github.com/micai98/ujelconfig-sauerbraten"
	guistrut 1
	guitext "I love writing scripts for dead games" 0
	guitext "the code is probably bad, I haven't written cubescript in ages, sorry" 0
	guibar 
	guistayopen [
		guibutton "reload ujelconfig" [ exec uconf.cfg ]
		guibutton "relaunch first-time setup" [exec uconf/recommended.cfg]
	]
	
	
] "misc"

uconf = [showgui "uconf"]