// part of ujelconfig by micai
// github.com/micai98/ujelconfig-sauerbraten

quickattack = [
	_qa_prevweapon = (getweapon)
	setweapon $arg1 0
	attack
	onrelease [
		setweapon $_qa_prevweapon 1
	]
]

quickattack_bindactions = ["quickattack fi" "quickattack sg" "quickattack cg" "quickattack rl" "quickattack ri" "quickattack gl" "quickattack pi"]


quickattack_gui_content = [
	guitext "bound keys will fire specified weapon while held"
	guitext "then switch back to the previous one when released"
	guibar
    _qa_ui_idx = 0
    guilistsplit n 2 $quickattack_bindactions [
        guilist [
            guitext (tabify (concatword (at $weaponnames2 $_qa_ui_idx) ": ") 4)
            [_qa_ui_newbinds@_qa_ui_idx] = (searchbinds $n)
            guikeyfield [_qa_ui_newbinds@_qa_ui_idx] -12 [
                oldbinds = (searchbinds [@@n])
                looplist j $oldbinds [bind $j ""]
                looplist j $[_qa_ui_newbinds@@_qa_ui_idx] [bind $j [@@@n]]
            ]
            _qa_ui_idx = (+ $_qa_ui_idx 1)
        ]
    ] [guibar]
]

newgui quickattack_gui quickattack_gui_content "quick attack"