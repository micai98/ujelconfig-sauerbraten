// part of ujelconfig by micai
// github.com/micai98/ujelconfig-sauerbraten

fakeweapon_attack_custom = [
	setweapon $arg2
	attack
	sleep 1 [ setweapon @arg1 1 ]
]

fakeweapon_attack = [
	fakeweapon_attack_custom $fakeweapon_fake $fakeweapon_real
]

fakeweapon_bindswap = [
	_fwep_fakebind = (searchbinds fakeweapon_attack)
	_fwep_realbind = (searchbinds attack)
	
	looplist item $_fwep_fakebind [
		bind $item attack
	]
	
	looplist item $_fwep_realbind [
		bind $item fakeweapon_attack
	]
]

fakeweapon_gui_content = [
	guitext "this feature is only really useful on instagib modes"
	guitext "it lets you make it look like you're firing rifle shots out of another weapon"
	guibar
	guitext "display: "
	guilist [
		guiradio "melee " fakeweapon_fake 0
		guiradio "shotgun " fakeweapon_fake 1
		guiradio "chaingun " fakeweapon_fake 2
		guiradio "rocket " fakeweapon_fake 3
		guiradio "rifle " fakeweapon_fake 4
		guiradio "grenade " fakeweapon_fake 5
		guiradio "pistol " fakeweapon_fake 6
	]
	guitext "shoot: "
	guilist [
		guiradio "melee " fakeweapon_real 0
		guiradio "shotgun " fakeweapon_real 1
		guiradio "chaingun " fakeweapon_real 2
		guiradio "rocket " fakeweapon_real 3
		guiradio "rifle " fakeweapon_real 4
		guiradio "grenade " fakeweapon_real 5
		guiradio "pistol " fakeweapon_real 6
	]
	guibar
	
	_fwep_idx = 0
    guilistsplit n 2 ["fakeweapon_attack" "attack"] [
        guilist [
            guitext (tabify (concatword $n ": ") 4)
            [newbinds@_fwep_idx] = (searchbinds $n)
            guikeyfield [newbinds@_fwep_idx] -12 [
                oldbinds = (searchbinds [@@n])
                looplist j $oldbinds [bind $j ""]
                looplist j $[newbinds@@_fwep_idx] [bind $j [@@@n]]
            ]
            _fwep_idx = (+ $_fwep_idx 1)
        ]
    ] [guibar]
	guistayopen [
		guibutton "swap binds" [fakeweapon_bindswap]
	]
] 

newgui fakeweapon_gui fakeweapon_gui_content "fake weapon"