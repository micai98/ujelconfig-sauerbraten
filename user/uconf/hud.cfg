// part of ujelconfig by micai
// github.com/micai98/ujelconfig-sauerbraten

// todo: make gamehud code not bad

gamehud_apply = [
	gamehud_string = ""
	if (= $gamehud_showfrags 1) [ gamehud_string = (concat $gamehud_string (concatword "^f7frags ^f" $gamehud_color "%1^n")) ]
	if (= $gamehud_showdeaths 1) [ gamehud_string = (concat $gamehud_string (concatword "^f7deaths ^f" $gamehud_color "%2^n")) ]
	if (= $gamehud_showkd 1) [ gamehud_string = (concat $gamehud_string (concatword "^f7k/d ^f" $gamehud_color "%3^n")) ]
	if (= $gamehud_showaccuracy 1) [ gamehud_string = (concat $gamehud_string (concatword "^f7acc ^f" $gamehud_color "%4%%^n")) ]
	if (= $gamehud_showflags 1) [ gamehud_string = (concat $gamehud_string (concatword "^f7flags ^f" $gamehud_color "%5^n")) ]
	if (= $gamehud_showfps 1) [ gamehud_string = (concat $gamehud_string (concatword "^f7fps ^f" $gamehud_color "%6^n")) ]
	gamehud = [result (if (! (isspectator $getclientnum)) [
		format $gamehud_string $getfrags $getdeaths (round (divf $getfrags (max (getdeaths) 1)) 0.1) $getaccuracy $getflags $getfps
	])]
]

gamehud_defaults = [
	gamehud_color = 2
	gamehud_showfrags = 1
	gamehud_showdeaths = 1
	gamehud_showkd = 1
	gamehud_showaccuracy = 1
	gamehud_showflags = 0
	gamehud_showfps = 0
	gamehud_apply
]

hud_gui_content = [
	guitext "scoreboard: ^f4(some settings don't apply to all gamemodes)"
	guilist [
		guitext "show: "
		guistayopen [
			if (= $hidefrags 0) [
				guibutton "frags" [hidefrags = 1] "checkbox_on" 
			] [
				guibutton "frags" [hidefrags = 0] "checkbox_off"
			]
		]
		guibar
		guicheckbox "deaths" showdeaths; guibar
		guicheckbox "clientnum" showclientnum; guibar
		guicheckbox "ping" showping; guibar
		guicheckbox "packetjump" showpj
	]
	guilist [
		guitext "show: "
		guicheckbox "spectators" showspectators; guibar
		guicheckbox "spectator ping" showspectatorping; guibar
		guicheckbox "server info" showservinfo; 
	]
	guibar
	guitext "gamehud: "
	guilist [
		guitext "color: "
		guiradio "^f0G" gamehud_color 0; guibar
		guiradio "^f1B" gamehud_color 1; guibar
		guiradio "^f2Y" gamehud_color 2; guibar
		guiradio "^f3R" gamehud_color 3; guibar
		guiradio "^f4D" gamehud_color 4; guibar
		guiradio "^f5M" gamehud_color 5; guibar
		guiradio "^f6O" gamehud_color 6; guibar
		guiradio "^f7W" gamehud_color 7; guibar
		guiradio "^f8C" gamehud_color 8
	]
	guistrut
	guilist [
		guitext "show: "
		guicheckbox "frags" gamehud_showfrags; guibar
		guicheckbox "deaths" gamehud_showdeaths; guibar
		guicheckbox "k/d" gamehud_showkd; guibar
		guicheckbox "acc" gamehud_showaccuracy; guibar
		guicheckbox "flags" gamehud_showflags; guibar
		guicheckbox "fps" gamehud_showfps
	]
	guistrut
	guistayopen [
		guilist [
			guibutton "apply gamehud settings" [ gamehud_apply ]
			guibar
			guibutton "restore default gamehud" [ gamehud_defaults ]
		]
	]
	guibar
    guilist [
		guicheckbox "vanilla FPS counter" showfps
		guibar
        guicheckbox "wall clock" wallclock
        if $wallclock [
            guibar
            guicheckbox "24 hour" wallclock24
            guibar
            guicheckbox "seconds" wallclocksecs
        ]
    ]
	guilist [
		guitext "console font scale: "
		guifield conscale 5
	]
	guibutton "change weapon models.." [ showgui hudgun_gui ]
]

hudgun_gui_content = [
	guibutton "default" [ hudgunsdir "" ]
	guibutton "ctf edition" [ hudgunsdir "hudguns_ctf" ]
	guibutton "classic enhanced" [ hudgunsdir "hudguns_classic_plus" ]
	guibutton "classic" [ hudgunsdir "hudguns_classic" ]
	guibutton "classic centered" [ hudgunsdir "hudguns_classic_center" ]
	guibutton "old enhanced " [ hudgunsdir "hudguns_old_plus" ]
	guibutton "old" [ hudgunsdir "hudguns_old" ]
	guibar
	guilist [
	guicheckbox "show hudguns" hudgun
		guibar
		guicheckbox "sway" hudgunsway
		guibar
		guicheckbox "muzzle flash" muzzleflash
		guibar
		guicheckbox "muzzle light" muzzlelight
	]
	guicheckbox "use chainsaw sounds" chainsawhudgun
	guibar
	guibutton "close" [cleargui] "exit"
]

newgui hud_gui hud_gui_content "hud & scoreboard"
newgui hudgun_gui hudgun_gui_content "hudguns"

if (! (getalias _uconf_setupcomplete_gamehud) 1) [
	gamehud_defaults
	_uconf_setupcomplete_gamehud = 1
]